# Maintainer: mrshankly <nonexistant@email.invalid>

_target=msp430-none-elf
pkgname="${_target}-headers"
pkgver=1.212
pkgrel=1
pkgdesc='Headers and support files for the MSP430 MCUs'
arch=('any')
url="https://www.ti.com/tool/MSP430-GCC-OPENSOURCE"
license=('custom')
options=(!strip)
source=("https://dr-download.ti.com/software-development/ide-configuration-compiler-or-debugger/MD-LlCjWuAbzH/9.3.1.2/msp430-gcc-support-files-${pkgver}.zip")
sha256sums=('3b1a39f10a344dfefb767e60ac35becef4c065013be86993195b138a5fb0b8d6')

package() {
  cd msp430-gcc-support-files/include

  install -dm755 "${pkgdir}/usr/${_target}/lib"
  install -m644 -t "${pkgdir}/usr/${_target}/lib" *.ld

  install -dm755 "${pkgdir}/usr/${_target}/include"
  install -m644 -t "${pkgdir}/usr/${_target}/include" *.h

  install -dm755 "${pkgdir}/usr/${_target}/include/devices"
  install -m644 -t "${pkgdir}/usr/${_target}/include/devices" devices.csv
}
